#!/usr/bin/expect

package require lcdproclib

set hasscreen 0
set lcdsid [::lcdproclib::spawnlcd "localhost" "13666"]
::lcdproclib::cmd ${lcdsid} "client_set -name remind"

proc scradd {} {
    variable lcdsid
    variable hasscreen

    if {$hasscreen} { return } 

    ::lcdproclib::cmd ${lcdsid} "screen_add 1"
    ::lcdproclib::cmd ${lcdsid} "screen_set 1 -name Reminders"
    ::lcdproclib::cmd ${lcdsid} "screen_set 1 -priority hidden"
    ::lcdproclib::cmd ${lcdsid} "screen_set 1 -cursor off"
    
    ::lcdproclib::cmd ${lcdsid} "widget_add 1 ti title"
    ::lcdproclib::cmd ${lcdsid} "widget_set 1 ti Reminders"
    
    ::lcdproclib::cmd ${lcdsid} "widget_add 1 v1 string"
    ::lcdproclib::cmd ${lcdsid} "widget_add 1 v2 string"
    ::lcdproclib::cmd ${lcdsid} "widget_add 1 v3 string"
    
    ::lcdproclib::cmd ${lcdsid} "screen_set 1 -priority info"

    set hasscreen 1
}

proc scrdel {} {
    variable lcdsid
    variable hasscreen

    if {[expr {!$hasscreen}]} { return } 

    ::lcdproclib::cmd ${lcdsid} "screen_del 1"
    set hasscreen 0
}

proc reload {lcdsid filename} {
  try {
    set fp [open ${filename} r]
  } trap {POSIX ENOENT} {} {
    puts "ENOENT"
    sleep 1
    return
  }

  set line1 [gets $fp]

  if {   [string equal "$line1" ""]
      || [string equal -nocase "$line1" "No Reminders."]} {
    scrdel
    return
  } else {
    scradd
    ::lcdproclib::cmd ${lcdsid} "widget_set 1 ti {$line1}"
    ::lcdproclib::cmd ${lcdsid} "widget_set 1 v1 1 2 {[gets $fp]}"
    ::lcdproclib::cmd ${lcdsid} "widget_set 1 v2 1 3 {[gets $fp]}"
    ::lcdproclib::cmd ${lcdsid} "widget_set 1 v3 1 4 {[gets $fp]}"
  }

  close ${fp}
}

proc spawninotify {filename} {
  spawn inotifywait -e CLOSE_WRITE,DELETE_SELF ${filename}
  wait -nowait -i ${spawn_id}
  return ${spawn_id}
}

set filename "/run/snakecontrol/remind.out"

# Ensure that the file exists before we go to watch it.
system "touch ${filename}"

set waitsid [spawninotify ${filename}]

while { true } {
  expect {
    -i ${waitsid} -re "(CLOSE|DELETE)" {
      close ${waitsid}
      set waitsid [spawninotify ${filename}]
    }
    -i ${waitsid} "Watches established." { reload ${lcdsid} ${filename} }
    -i ${lcdsid} -re "^listen \[^\n\]*\n" {}
    -i ${lcdsid} -re "^ignore \[^\n\]*\n" {}
    -i ${lcdsid} -re "\[^\n\]*\n" {}
  }
}
